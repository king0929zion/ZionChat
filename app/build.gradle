plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.plugin.compose'
}

def escapeForBuildConfig = { value ->
    return value.toString().replace("\\", "\\\\").replace("\"", "\\\"")
}

def runtimePackagerBaseUrl =
    (project.findProperty("RUNTIME_PACKAGER_BASE_URL")
        ?: System.getenv("RUNTIME_PACKAGER_BASE_URL")
        ?: "http://127.0.0.1:17856").toString()
def runtimePackagerToken =
    (project.findProperty("RUNTIME_PACKAGER_TOKEN")
        ?: System.getenv("RUNTIME_PACKAGER_TOKEN")
        ?: "").toString()

android {
    namespace 'com.zionchat.app'
    compileSdk 36

    defaultConfig {
        applicationId "com.zionchat.app"
        minSdk 24
        targetSdk 36
        versionCode 176
        versionName "0.18.143"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        buildConfigField "String", "RUNTIME_PACKAGER_BASE_URL", "\"${escapeForBuildConfig(runtimePackagerBaseUrl)}\""
        buildConfigField "String", "RUNTIME_PACKAGER_TOKEN", "\"${escapeForBuildConfig(runtimePackagerToken)}\""
    }

    def signingKeystorePath = System.getenv("SIGNING_KEYSTORE_PATH")
    def signingStorePassword = System.getenv("SIGNING_STORE_PASSWORD")
    def signingKeyAlias = System.getenv("SIGNING_KEY_ALIAS")
    def signingKeyPassword = System.getenv("SIGNING_KEY_PASSWORD")
    def hasSigning = signingKeystorePath && signingStorePassword && signingKeyAlias && signingKeyPassword

    signingConfigs {
        release {
            if (hasSigning) {
                storeFile file(signingKeystorePath)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            if (hasSigning) {
                signingConfig signingConfigs.release
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.10.0'
    implementation 'androidx.activity:activity-compose:1.12.3'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation platform('androidx.compose:compose-bom:2026.01.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.navigation:navigation-compose:2.9.7'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.10.0'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.datastore:datastore-preferences:1.2.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    implementation 'io.coil-kt.coil3:coil-compose:3.3.0'
    implementation 'io.coil-kt.coil3:coil-network-okhttp:3.3.0'
    implementation 'io.coil-kt.coil3:coil-svg:3.3.0'
    implementation 'org.commonmark:commonmark:0.21.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.10.0'
    implementation 'io.ktor:ktor-client-core:3.4.0'
    implementation 'io.ktor:ktor-client-okhttp:3.4.0'
    implementation 'io.ktor:ktor-client-content-negotiation:3.4.0'
    implementation 'io.ktor:ktor-sse:3.4.0'
    implementation 'io.ktor:ktor-serialization-kotlinx-json:3.4.0'
    implementation 'io.modelcontextprotocol:kotlin-sdk:0.8.3'
    implementation 'io.github.kyant0:backdrop:1.0.5'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2026.01.01')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}
